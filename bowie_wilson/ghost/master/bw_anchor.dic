/*
 █████  ███    ██  ██████ ██   ██  ██████  ██████  
██   ██ ████   ██ ██      ██   ██ ██    ██ ██   ██ 
███████ ██ ██  ██ ██      ███████ ██    ██ ██████  
██   ██ ██  ██ ██ ██      ██   ██ ██    ██ ██   ██ 
██   ██ ██   ████  ██████ ██   ██  ██████  ██   ██                                               
 */

//--OnTranslate

//As I said above, OnTranslate filters through all the text your ghost displays and does whatever you specify to it. That doesn't have to just be turning things into links, it could be anything. This is a powerful function that can be used in a lot of creative ways I bet, but I wouldn't mess with it too much since you can pretty easily break stuff if you don't know what you're doing.
//If you're not interested in any anchor words, delete all the lines that start with _talk = REPLACE under OnTranslate.

OnTranslate
	{
	_talk = reference0
 	// _talk = REPLACE(_talk, "anlamingtons", "\_a[lamingtons]lamingtons\_a")
 	// _talk = REPLACE(_talk, "anpavlova", "\_a[pavlova]pavlova\_a")
 	// _talk = REPLACE(_talk, "anfairybread", "\_a[fairybread]fairy bread\_a")
	
		if reference1 == "" && reference2 == "" { //send input box : no event (ref2) , no special flag (ref1)
            EVAL('"' + REPLACE(_talk,'"','" + CHR(0x22) + "') + '"')
        }
        else { //event result from ghost
            TOSTR(_talk)
        }
	}
	
	
//--OnAnchorSelect
//OnAnchorSelect runs whenever someone clicks one of the links as specified above, or a link that begins with http://.

OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0 //checking to see if the link is a web address
	{
	"\j[%(reference0)]\e" // \j is a tag that will open a specified url in the user's browser of choice. This basically just tells the ghost to open the webpage stored in reference0, which would be the address that the user just clicked. I didn't get too much into \j in the walkthrough page because I have literally never used it except here.
	}
	else //if it's NOT a web address
	{
	case reference0 //case is a unique thing that only really shows up in anchor.dic, and it basically sets up reference0 for evaluation to see if matches any of the below criteria, which are all tagged with when. You can read more about this on the coding page on the walkthrough site. You can add as many things here as you want to set up above as long as you follow the basic format.
		{
			when "lamingtons" //if the link is this value
			{
				"\0\s[0]Oh,\w8 you don't know what lamingtons are?\w8\n\nAh,\w8 I suppose they are an Australian thing...\w8\nLets see here...\w8\n\nThe dictionary definition is\n'a square of sponge cake\ndipped in melted chocolate\nand grated coconut.'\n\n[half]/
				\![*]\q[Click for image,lamingtonimage]\n\n/
				\![*]\q[Back?,BACKTOMENU]\n/
	            \![*]\q[Nevermind,closemenu]\n"
			}
			when "pavlova" //if the link is this value
			{
				"\0\s[0]Hm?\w8 Oh pavlova is an aussie/nz thing!\w8 This definition is 'a dessert consisting of a meringue base or shell filled with whipped cream and fruit.'\n\n[half]/
				\![*]\q[Click for image,pavlovaimage]\n\n/
				\![*]\q[Back?,BACKTOMENU]\n/
	            \![*]\q[Nevermind,closemenu]\n"
			}
			when "fairybread" //if the link is this value
			{
				"\0\s[0]ReplaceDialogue\n\n[half]/
				\![*]\q[Click for image,fairybreadimage]\n\n/
				\![*]\q[Back?,BACKTOMENU]\n/
	            \![*]\q[Nevermind,closemenu]\n"
			}
		}
	}
}

Select.lamingtonimage
{
	"\![set,autoscroll,disable]\_q\_b[imgs\lamingtonsresized.png,centre,--option=use_self_alpha]\n\n\n\n\n\n\n\n/
	\f[sub,true]Credit to Wandercooks.com for the image\f[sub,default]\n\n/
	\![*]\q[Back?,BACKTOMENU]\n/
	\![*]\q[Nevermind,closemenu]\n"
}

Select.pavlovaimage
{
	"\![set,autoscroll,disable]\_q\_b[imgs\pavlovaresized.png,centre,--option=use_self_alpha]\n\n\n\n\n\n/
	\f[sub,true]Credit to Goodfood.com.au for the image\f[sub,default]\n\n/
	\![*]\q[Back?,BACKTOMENU]\n/
	\![*]\q[Nevermind,closemenu]\n"
}

Select.fairybreadimage
{
	"\![set,autoscroll,disable]\_q\_b[imgs\fairybreadresized.png,centre,--option=use_self_alpha]\n\n\n\n\n\n\n\n/
	\f[sub,true]Credit to Sweetestmenu.com for the image\f[sub,default]\n\n/
	\![*]\q[Back?,BACKTOMENU]\n/
	\![*]\q[Nevermind,closemenu]\n"
}